include: "run_common.snake"

rule step_JobReport:
    params:
        cfgurl = cfgurl
    input: 
        env = rules.scram.output.env,
        report_RAW2DIGI = {rules.step_RAW2DIGI.output.report},
        report_DQM = {rules.step_DQM.output.report}
    output:
        report = "jobreport.json"
    shell: 
        """
        source {input.env}
        python $CMSSW_BASE/src/HGCalCommissioning/Configuration/test/jobReportBuilder.py \
            --initialcfg {params.cfgurl} \
            --fwkreports {input.report_RAW2DIGI},{input.report_DQM} \
            -o {output.report}
        """

rule endstep:
     input :
           rules.step_RAW2DIGI.output,
           rules.step_DQM.output,
           rules.step_JobReport.output
     params:
        mycopy = "cp -v" if f'{job_dict["OutputDir"]}'.find('/eos/cms/')<0 else "eos root://eoscms.cern.ch cp",
        outdir = f'{job_dict["OutputDir"]}',
        tag = f'{job_dict["Run"]}_{job_dict["LumiSection"]}',
        dqmtag = f'V{job_dict["LumiSection"]:04d}_HGCAL_R{job_dict["Run"]:09d}'
     output:
        log = "endstep.txt"
     shell:
        """
        #ROOT files
        {params.mycopy} {rules.step_RAW2DIGI.output.root} {params.outdir}/RAW2DIGI_{params.tag}.root > {output.log}
        {params.mycopy} {rules.step_DQM.output.root} {params.outdir}/DQM_{params.dqmtag}.root >> {output.log}
        #Reports (copy only the final report)
        {params.mycopy} {rules.step_JobReport.output.report} {params.outdir}/reports/job_{params.tag}.json >> {output.log}
        """

rule all:
    input:
        rules.scram.output,
        rules.step_RAW2DIGI.output,
        rules.step_DQM.output,
        rules.step_DQM_upload.output,
        rules.endstep.output