import json
import os
import re

snakefile_dir = os.path.dirname(workflow.basedir)

cfgurl=workflow.config_settings.configfiles[0]
with open(cfgurl,'r') as cfg:
    job_dict = json.load(cfg)
maxEvents=-1

workdir: 
    job_dict['LocalDir'] if 'LocalDir' in job_dict else os.getcwd()
    
rule scram:
    params :
        cmsswpath = re.findall( "(.*CMSSW_.*/src/)", snakefile_dir )[0]
    output:
        env = "cmssw_env.sh",
        check = "scramdone.txt"
    shell:
        """
        echo "CMSSW base will be set to {params.cmsswpath}" > {output.check}
        echo "Creating script {output.env}" >> {output.check}
        echo "#!/bin/bash" > {output.env}
        echo "[ -z \"$CMS_PATH\" ] && source /cvmfs/cms.cern.ch/cmsset_default.sh" >> {output.env}
	    echo "export SITECONFIG_PATH=/cvmfs/cms.cern.ch/SITECONF/T2_CH_CERN" >> {output.env}
        echo "cd {params.cmsswpath}" >> {output.env}
        echo "eval \\`scramv1 runtime -sh\\`" >> {output.env}
        echo "cd -" >> {output.env}
        source {output.env}
        echo "CMS_PATH=$CMS_PATH" >> {output.check}
        echo "CMSSW_BASE=$CMSSW_BASE" >> {output.check}
        """

##
## UNPACKING WORKFLOWS
##

rule step_RAW2DIGI:
    params:        
        run = f'{job_dict["Run"]}',
        lumi = f'{job_dict["LumiSection"]}',
        era = f'{job_dict["RecoEra"]}',
        inputFiles = ','.join(job_dict["Inputs"][1:]),
        inputTrigFiles = ','.join( [job_dict["Inputs"][0],] ),
        yamls = f'\"{job_dict["YAMLs"]}\"',
        cfg = "$CMSSW_BASE/src/HGCalCommissioning/Configuration/test/step_RAW2DIGI.py",
        maxEvents = maxEvents
    input: 
        env = rules.scram.output.env
    output:
        root = "RAW2DIGI.root",
        report = "FrameworkJobReport_RAW2DIGI.xml"
    shell: 
        """
        source {input.env}
        cmsRun -j {output.report} \
               {params.cfg} run={params.run} lumi={params.lumi} era={params.era} \
               files={params.inputFiles} inputTrigFiles={params.inputTrigFiles} yamls={params.yamls} \
               output={output.root} maxEvents={params.maxEvents}
        #CMSSW appends numEvents to the file name if maxEvents!=-1
        #force the file name to be always the same
        #targetout={output.root}
        #localout=${{targetout/.root/*.root}}
        #mv -v $localout $targetout
        """

##
## DQM WORKFLOWS
##
rule step_DQM:
    params:
        run = f'{job_dict["Run"]}',
        era = f'{job_dict["RecoEra"]}',
        cfg = "$CMSSW_BASE/src/HGCalCommissioning/Configuration/test/step_DQM.py",
        dqm = f'DQM_V0001_HGCAL_R{job_dict["Run"]}.root'
    input: 
        env = rules.scram.output.env,
        root = rules.step_RAW2DIGI.output.root
    output:
        report = "FrameworkJobReport_DQM.xml",
        root = "DQM.root"
    shell: 
        """
        source {input.env}
        cmsRun -j {output.report} \
               {params.cfg} run={params.run} era={params.era} files=file:{input.root} maxEvents=-1
        mv {params.dqm} {output.root}
        """

rule step_DQM_upload:
    params:
        run = f'{job_dict["Run"]}',
        dqmtag = f'V{job_dict["LumiSection"]:04d}_HGCAL_R{job_dict["Run"]:09d}',
        dqmuploadtag = f'V0001_HGCAL_R{job_dict["Run"]:09d}'
    input:
        env = rules.scram.output.env,
        root = rules.step_DQM.output.root
    output:
        report = "dqmupload.done"
    shell: 
        """
        source {input.env}
        cp {input.root} DQM_{params.dqmtag}.root
	cp {input.root} DQM_{params.dqmuploadtag}.root
	visDQMUpload.py http://dev-hgcal-dqm-vm.cern.ch:8070/dqm/online-dev DQM_{params.dqmuploadtag}.root > {output.report}
	rm DQM_{params.dqmuploadtag}.root
        echo "Upload is disabled for the moment while validating" > {output.report}
        """

##
## RECO WORKFLOWS
##
rule step_RECO:
    params:
        era = f'{job_dict["RecoEra"]}',
        root = f'{job_dict["Inputs"]}',
        cfg = "$CMSSW_BASE/src/HGCalCommissioning/Configuration/test/step_RECO.py"
    input: 
        env = rules.scram.output.env
    output:
        report = "FrameworkJobReport_RECO.xml",
        root = "RECO.root"
    shell: 
        """
        source {input.env}
	cmsRun -j {output.report} \
	       {params.cfg} era={params.era} files=file:{params.root} maxEvents=-1 gpu=false output={output.root}
        """

##
## NANO WORKFLOWS
##
rule step_RAW2NANO:
    params:       
        era = f'{job_dict["RecoEra"]}',
        cfg = "$CMSSW_BASE/src/HGCalCommissioning/Configuration/test/step_NANO.py"
    input: 
        env = rules.scram.output.env,
        root = rules.step_RAW2DIGI.output.root
    output:
        report = "FrameworkJobReport_NANO.xml",
        root = "NANO.root"
    shell: 
        """
        source {input.env}
        cmsRun -j {output.report} \
               {params.cfg} era={params.era} files=file:{input.root} skipRecHits=true maxEvents=-1
        """
	
rule step_RECO2NANO:
    params:
        era = f'{job_dict["RecoEra"]}',
        cfg = "$CMSSW_BASE/src/HGCalCommissioning/Configuration/test/step_NANO.py"
    input: 
        env = rules.scram.output.env,
        root = rules.step_RECO.output.root
    output:
        report = "FrameworkJobReport_RECO2NANO.xml",
        root = "RECO2NANO.root"
    shell: 
        """
        source {input.env}
        cmsRun -j {output.report} \
               {params.cfg} era={params.era} files=file:{input.root} skipRecHits=false maxEvents=-1
	mv NANO.root {output.root}
        """